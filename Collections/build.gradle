plugins {
    id 'java'
    id 'idea'
}

group 'org.example'
version '1.0-SNAPSHOT'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    maven {
        url "https://maven.pkg.github.com/QilinPTA/Qilin"
        credentials {
            username = project.findProperty("gpr.user") ?: "QilinPTA"
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    implementation "qilin:pta:1.0-SNAPSHOT"
    implementation "qilin:core:1.0-SNAPSHOT"
    implementation "qilin:util:1.0-SNAPSHOT"
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.2'
    implementation files("${rootDir}/libs/sootclasses-4.2.1-jar-with-dependencies.jar")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

task fatJar(type: Jar, dependsOn: ['build']) {
    manifest {
        attributes(
                'Main-Class': 'Main',
        )
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test {
    useJUnitPlatform()
}